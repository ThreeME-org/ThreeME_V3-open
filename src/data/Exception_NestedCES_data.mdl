########################################################
##    CALIBRATION OF NESTED CES PRODUCTION FUNCTION   ##
########################################################
### This file defines the calibration a nested CES production function. The nested structure is a follows: K (capital), L (labor), E (energy), M (material).
###
###        Y
###		  / \    --> ES_NEST_MAT_KEL
###		 M	KEL
###			/ \  --> ES_NEST_KE_L
###		   KE  L
###		   /\    --> ES_NEST_K_E
###		  K  E
###
### The speficication of the ES used between two inputs is the one presented in Reynès (2018, Appendix B):
### Reynès, F. (2019), "The Cobb-Douglas function as a flexible function. A new perspective on homogeneous functions through the lens of output elasticities", Mathematical Social Sciences 97, 11-17.

# Elasticity of Substitution (ES) between material and the agregate capital-energy-labor
ES_NEST_MAT_KEL[s] := 0.0

# ES between the agregate capital-energy and labor
ES_NEST_KE_L[s] := 0.5

# ES between the agregate capital and energy (within the agregate capital-energy)
# It is important to notice that this ES is the ES within the agregate capital-energy. The actual ES depends on the ES in other parts of the nest and on input shares.
ES_NEST_K_E[s] := 0.2


# From the ES in the different levels of the nest, it is possible the actual elasticity between two inputs. Since the ES between two inputs is symetric, we have:

# ES between material and capital
@over ES[MAT, K, s] := ES_NEST_MAT_KEL[s]
@over ES[K, MAT, s] := ES[MAT, K, s]

# ES between material and energy
@over ES[MAT, E, s] := ES_NEST_MAT_KEL[s]
@over ES[E, MAT, s] := ES[MAT, E, s]

# ES between material and labor
@over ES[MAT, L, s] := ES_NEST_MAT_KEL[s]
@over ES[L, MAT, s] := ES[MAT, L, s]

# ES between capital and energy
@over ES[K, E, s] := ES_NEST_K_E[s]/(1 - phi[MAT, s] - phi[L, s]) - ES_NEST_MAT_KEL[s]/(1 - phi[MAT, s]) - ES_NEST_KE_L[s] * phi[L, s] / (1 - phi[MAT, s]) / (1 - phi[MAT, s] - phi[L, s])
@over ES[E, K, s] := ES[K, E, s]


# ES between capital and labor
@over ES[K, L, s] := (ES_NEST_KE_L[s] - ES_NEST_MAT_KEL[s] * phi[MAT, s]) / (1 - phi[MAT, s])
@over ES[L, K, s] := ES[K, L, s]

# ES between energy and labor
@over ES[E, L, s] := ES[K, L, s]
@over ES[L, E, s] := ES[E, L, s]
