include ..\model\lists

#####################
###### USE SIDE #####
#####################

Q|O|[c] := MGR|O|[c] + CI|O|[c] + CH|O|[c] + G|O|[c] + I|O|[c] + X|O|[c] + DS|O|[c]

CI|O|[c] := sum(CI|O|[c, s] on s)

I|O|[c] := sum(I|O|[c, s] on s)

CI[c, s] := CID[c, s] + CIM[c, s]

|V|[c] := |V|D[c] + |V|M[c] where V in Q CI CH G I X DS

MGRbis[c] := MGRD[c] + MGRM[c]

|V||O| := sum(|V||O|[c] on c) where V in Q MGR CI CH G I X DS

|V| := |V|D + |V|M where V in Q MGR CI CH G I X DS

#####################
###### SUPPY SIDE ###
#####################

MGP|O|[c] := sum(MGP|O|[cc, c] on cc)

# YQ[c] can not be calibrated from equation because circular reference with NTAXCD_VAL[c] that depends on YQ[c]. Use instead YQ[c] := sum(Y[c, s] on s)
# YQ[c]*PYQ[c] + NTAXCD_VAL[c] + PMGPD[c]*MGPD[c] := PQD[c]*QD[c]

YQbis[c] + NTAXCD[c] + MGPD[c] := QD[c]

# M[c] can not be calibrated from equation because circular reference with NTAXCM_VAL[c] that depends on M[c]. Use round 0 instead.
# M[c]*PM[c] + NTAXCM_VAL[c] + PMGPM[c]*MGPM[c] := PQM[c]*QM[c]

Mbis[c] + NTAXCM[c] + MGPM[c] := QM[c]

MGPD := sum(MGPD[c] on c) 

MGPM := sum(MGPM[c] on c) 

MGR[cc] := sum(MGP[cc, c] on c)

MGP[cc, c] := MGPD[cc, c] + MGPM[cc, c]

YQ := sum(YQ[c] on c)

M := sum(M[c] on c)

NTAXC_VAL[c] := NTAXCD_VAL[c] + NTAXCM_VAL[c]

NTAXC[c] := NTAXCD[c] + NTAXCM[c]

NTAXC := sum(NTAXC[c] on c)

CI|O|[s] := sum(CI|O|[c, s] on c)

CI[s] := CID[s] + CIM[s]

CIbis := sum(CI[s] on s)

I|O|[s] := sum(I|O|[c, s] on c)

I[s] := ID[s] + IM[s]

Ibis := sum(I[s] on s)

Y[s] := sum(Y[c, s] on c)

Y := sum(Y[s] on s)

VA[s] := Y[s] - CI[s]

VA_VAL[s] := PY[s] * Y[s] - PCI[s] * CI[s]

VA := sum(VA[s] on s)

WAGES := sum(WAGES[s] on s)

NTAXP_VAL := sum(NTAXP_VAL[s] on s)

NTAXP := sum(NTAXP[s] on s)

GOS_VAL[s] := VA_VAL[s] - PWAGES[s] * WAGES[s] - NTAXP_VAL[s]

GOS[s] := VA[s] - WAGES[s] - NTAXP[s]

GOS := sum(GOS[s] on s)

NOS_VAL[s] := GOS_VAL[s] - PK[s]{-1} * Tdec[s] * K[s]{-1}

NOS[s] := GOS[s] - @elem(PK[s]{-1}, %baseyear) * Tdec[s] * K[s]{-1}

NOS := sum(NOS[s] on s)

### GDP definitions

GDP := CH + G + I + X + DS - M

GDP[c] := CH[c] + G[c] + I[c] + X[c] + DS[c] - M[c]

GDPbis := sum(GDP[c] on c)

GDPter := VA + NTAXC
