# BLOCK 6 : GREEN HOUSE GASES EMISSIONS

include ..\model\lists

# ***CO2 SECTORS EMISSIONS***
# equation 6.1: CO2 emissions per sector and source
  d(log(EMS_SEC[ce2,s])) = (@year>%baseyear)*d(log(E[ce2,s]*IC[ce2,s])) + (@year=<%baseyear)*log(1 + STEADYSTATE(2,1)) if EMS_SEC[ce2,s] <> 0

# equation 6.2: CO2 emissions from decarbonation per sector 
  d(log(EMS_DC[s])) = d(log(MAT[s])) if EMS_DC[s] <> 0

# equation 6.3: CO2 emissions per household and source  
  d(log(EMS_HH[ce2,h])) = (@year>%baseyear)*d(log(EXP[ce2,h]*IC_HH[ce2,h])) + (@year=<%baseyear)*log(1 + STEADYSTATE(2,1)) if EMS_HH[ce2,h] <> 0

# ***AGREGATION OF CO2 EMISSIONS***
# equation 6.4: CO2 emissions per sector
  EMS_SEC[s] = sum(EMS_SEC[ce2,s] if EMS_SEC[ce2,s] <> 0 on ce2) if EMS_SEC[s] <> 0

# equation 6.5: CO2 emissions of all sectors
  EMS_SEC = sum(EMS_SEC[s] if EMS_SEC[s] <> 0 on s)

# equation 6.6: CO2 emissions from decarbonation of all sectors   
  EMS_DC = sum(EMS_DC[s] if EMS_DC[s] <> 0 on s)  

# equation 6.7: CO2 emissions per source of all sectors
  EMS_SECSOU[ce2] = sum(EMS_SEC[ce2,s] if EMS_SEC[ce2,s] <> 0 on s)
 
# equation 6.8: CO2 emissions of all sectors
  EMS_SECSOU = sum(EMS_SECSOU[ce2] if EMS_SECSOU[ce2] <> 0 on ce2)  
  Verif_EMS = EMS_SEC - EMS_SECSOU
  
# equation 6.9: CO2 emissions all households per source
  EMS_HH[ce2] = sum(EMS_HH[ce2, h] on h)

# equation 6.10: CO2 emissions all households
  EMS_HH = sum(EMS_HH[ce2] on ce2)

# equation 6.11: Total CO2 emissions 
  EMS_TOT = EMS_SEC + EMS_DC + EMS_HH

  EMS_SOU = EMS_SECSOU + EMS_HH
  EMS_SOU[ce2] = EMS_SECSOU[ce2] + EMS_HH[ce2]
  
  verif_EMS_SOU = sum(EMS_SOU[ce2] on ce2) - EMS_SOU   