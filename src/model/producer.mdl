########################################################
#########         PRODUCER         #####################
########################################################
### This file provides the equations defining the producer behaviour.
### Equation are behavioral. They are not used to calibration variable. They may be inverted to calibrate a parameter. 

### Margins paid to commodity cc on the domestic commodity c 
d(log(MGPD[cc, c])) = d(log(YQ[c])) + d(SUBST_MGPD[cc, c]) if MGPD[cc, c] <> 0

SUBST_n_MGPD[cc, c] = sum(- ES_MGPD[cc, ccc, c] * phi_MGPD[ccc, c]{-1} * d(log(PMGPD[cc, c]) - log(PMGPD[ccc, c])) if MGPD[ccc, c] <> 0 on ccc) if MGPD[cc, c] <> 0

phi_MGPD[cc, c] = PMGPD[cc, c] * MGPD[cc, c] / (sum(PMGPD[ccc, c] * MGPD[ccc, c] on ccc)) if MGPD[cc, c] <> 0

### Margins paid to commodity cc on the imported commodity c 
d(log(MGPM[cc, c])) = d(log( M[c])) + d(SUBST_MGPM[cc, c]) if MGPM[cc, c] <> 0

SUBST_n_MGPM[cc, c] = sum(- ES_MGPM[cc, ccc, c] * phi_MGPM[ccc, c]{-1} * d(log(PMGPM[cc, c]) - log(PMGPM[ccc, c])) if MGPM[ccc, c] <> 0 on ccc) if MGPM[cc, c] <> 0

phi_MGPM[cc, c] = PMGPM[cc, c] * MGPM[cc, c] / (sum(PMGPM[ccc, c] * MGPM[ccc, c] on ccc)) if MGPM[cc, c] <> 0

### Production of commodity c by sector s 
Y[c, s] = PhiY[c, s] * YQ[c] if Y[c, s] <> 0


## Demand for production factor f of sector s
d(log(F_n[f, s])) = d(log(Y[s])) - d(log(PROG[f, s])) + d(SUBST_F[f, s])  if F_n[f, s] <> 0

d(SUBST_n_F[f, s]) = sum(- ES[f, ff, s] * phi[ff, s]{-1} * d(log(C[f, s]/PROG[f, s]) - log(C[ff, s]/PROG[ff, s])) on ff) if F_n[f, s] <> 0

phi[f, s] = C[f, s] * F_n[f, s] / (sum(C[ff, s] * F_n[ff, s] on ff))

# Agregate production factors for capital, labor, energy and material
#### use list f !!!!!!@@@!!! 
#### F[f] = sum(F[f, s] on s)
F[K] = sum(F[K, s] on s)
F[L] = sum(F[L, s] on s)
F[E] = sum(F[E, s] on s)
F[MAT] = sum(F[MAT, s] on s)

## Investment in commodity c by sector s
## For a given sector, we assume that the investment structure is fixed over time. In other words, the investment good is a composite of several commodities in fixed proportion. 
d(log(I[c, s])) = d(log(IA[s])) if I[c, s] <> 0


## Energy demand by type of energy in sector s
d(log(CI[ce, s])) = d(log(F[E, s])) + d(SUBST_CI[ce,s]) if CI[ce, s] <> 0

d(SUBST_n_CI[ce, s]) = sum(- ES_NRJ[ce, cee, s] * phi[E, cee, s]{-1} * d(log(PCI[ce, s]) - log(PCI[cee, s])) on cee) if CI[ce, s] <> 0

phi[E, ce, s] = PCI[ce, s]*CI[ce, s] / (sum(PCI[cee, s]*CI[cee, s] on cee))

## Demand for material commodity cmo by sector s
## Intermediary consumption that are not transport or energy commodities
## Leontief hypothesis
d(log(CI[cmo, s])) = d(log(F[MAT, s])) if CI[cmo, s] <> 0

### Demand for transport commodities by sector s
## Leontief hypothesis
d(log(TRSP[s])) = d(log(F[MAT, s])) if TRSP[s] <> 0

## Demand for transport commodity ct by sector s
d(log(CI[ct, s])) = d(log(TRSP[s])) + d(SUBST_CI[ct,s]) if CI[ct, s] <> 0

d(SUBST_n_CI[ct, s]) = sum(- ES_TRSP[ct, ctt, s] * phi_TRSP[ctt, s]{-1} * d(log(PCI[ct, s]) - log(PCI[ctt, s])) on ctt) if CI[ct, s] <> 0

phi_TRSP[ct, s] = PCI[ct, s]*CI[ct, s] / (sum(PCI[ctt, s]*CI[ctt, s] on ctt))


## Technical progress of the production factor f

PROG[f, s] = PROG[f, s]{-1} * (1 + GR_PROG[f, s])

## Endogenous energy efficiency
GR_PROG[E, s] = @elem(GR_PROG[E, s], %baseyear) + RHO_PROG_E_PE * (log(PE[s]) - log(P) > 0) * d(log(PE[s])-log(P))

## Doute sur le specification ci-dessus qui reprend celle existante:
# GR_PROG_E[s] =  alpha_PROG_E[s] + alpha_PROG_E_PE*((log(PE[s])- log(PCH)>0)*d(log(PE[s])-log(PCH)))
### On a une variation qui affecte un niveau. L'effet doit être faible.
## Je serais tenter de mettre plutot (à discuter!!!):
#GR_PROG[E, s] = @elem(GR_PROG[E, s], %baseyear) + RHO_PROG_E_PE * (log(PE[s]) - log(P) > 0) * (log(PE[s])-log(P))