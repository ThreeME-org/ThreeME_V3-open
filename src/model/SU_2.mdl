#### Supply-Use equilibrium

## This section provides the equations defining the supply use - equilibrium for the domestic and imported products and at the agregate level. It also derives the GDP according to various definitions. Since each relation is written in value and in volume, the value equation defines the price.

### Use side

# Domestic and foreign equilibrium for commodities $c$ (value & volume):

## Market price for the domestically produced commodity $c$
PQD[c]  * QD[c]  = PMGRD[c] * MGRD[c] + PCID[c] * CID[c] + PCHD[c] * CHD[c] + PGD[c] * GD[c] + PID[c] * ID[c] + PXD[c] * XD[c] + PDSD[c] * DSD[c] if QD[c] <> 0

## Quantity of domestically produced commodity $c$ expressed at market price
		  QD[c]  = 	          MGRD[c] + 		  CID[c] + 		     CHD[c] + 	   	   GD[c] + 		    ID[c] + 		 XD[c] + 		   DSD[c] if QD[c] <> 0

## Market price for imported commodity $c$
PQM[c]  * QM[c]  = PMGRM[c] * MGRM[c] + PCIM[c] * CIM[c] + PCHM[c] * CHM[c] + PGM[c] * GM[c] + PIM[c] * IM[c] + PXM[c] * XM[c] + PDSM[c] * DSM[c] if QM[c] <> 0

## Quantity of imported commodity $c$ expressed at market price
		  QM[c]  = 		      MGRM[c] + 		  CIM[c] + 		     CHM[c] + 	       GM[c] + 		    IM[c] + 		 XM[c] + 		   DSM[c] if QM[c] <> 0

# Domestic and imported intermediary raw material consumption c (value & volume):

## Market price for the domestically produced commodity $c$ for intermediary consumption use
PCID[c] * CID[c] = sum(PCID[c, s] * CID[c, s] if CID[c, s] <> 0 on s) if CID[c] <> 0

## Quantity of domestically produced commodity $c$ for intermediary consumption use expressed at market price
		  CID[c] = sum( 			CID[c, s] if CID[c, s] <> 0 on s) if CID[c] <> 0

## Market price for imported commodity $c$ for intermediary consumption use
PCIM[c] * CIM[c] = sum(PCIM[c, s] * CIM[c, s] if CIM[c, s] <> 0 on s) if CIM[c] <> 0

## Quantity of imported commodity $c$ for intermediary consumption use expressed at market price
		  CIM[c] = sum( 			CIM[c, s] if CIM[c, s] <> 0 on s) if CIM[c] <> 0


## Market price for domestically produced commodity $c$ for investment use
PID[c]  * ID[c]  = sum(PID[c, s]  * ID[c, s]  if ID[c, s]  <> 0 on s) if ID[c]  <> 0

## Quantity of imported commodity $c$ for investment use expressed at market price
		  ID[c]  = sum( 		    ID[c, s]  if ID[c, s]  <> 0 on s) if ID[c]  <> 0

## Market price for imported commodity $c$ for investment use
PIM[c]  * IM[c]  = sum(PIM[c, s]  * IM[c, s]  if IM[c, s]  <> 0 on s) if IM[c]  <> 0

## Quantity of imported commodity $c$ for investment use expressed at market price
		  IM[c]  = sum( 		    IM[c, s]  if IM[c, s]  <> 0 on s) if IM[c]  <> 0


# Aggregation of imports and domestic production for commodity $c$ per use
# For $Q$ (production of commodities at market price); $CI$ (intermediary consumption); $I$ (private investiment); $DS$ (change in inventories)
## Market price of commodity $c$
PQ[c]  * Q[c]    = PQD[c]  * QD[c]  + PQM[c]  * QM[c]                 if Q[c]   <> 0

## Quantity of commodity $c$ expressed at market price
		 Q[c]    = 		     QD[c]  +			QM[c]                 if Q[c]   <> 0

## Market price of commodity $c$ for intermediate consumption use
PCI[c] * CI[c]   = PCID[c] * CID[c] + PCIM[c] * CIM[c]                if CI[c]  <> 0

## Quantity of commodity $c$ expressed at market price for intermediate consumption use
		 CI[c]   = 		     CID[c] + 		    CIM[c]                if CI[c]  <> 0

## Market price of commodity $c$ for investment use
PI[c]  * I[c]    = PID[c]  * ID[c]  + PIM[c]  * IM[c]                 if I[c]   <> 0

## Quantity of commodity $c$ expressed at market price for investment use
		 I[c]    = 		     ID[c]  + 		    IM[c]                 if I[c]   <> 0

## Market price of commodity $c$ for change in inventories use
PDS[c] * DS[c]   = PDSD[c] * DSD[c] + PDSM[c] * DSM[c]                if DS[c]  <> 0

## Quantity of commodity $c$ expressed at market price for change in inventories use
		 DS[c]   = 		     DSD[c] + 		    DSM[c]                if DS[c]  <> 0

# Aggregation of imports and domestic production per use
## Aggregate market price for domestically produced commodities
PQD   * QD   = sum(PQD[c]   * QD[c]   if QD[c]  <> 0 on c)  		  if QD  	<> 0

## Total quantity of domestically produced commodities
	    QD   = sum( 		  QD[c]   if QD[c]  <> 0 on c) 			  if QD  	<> 0

## Aggregate market price for imported commodities
PQM   * QM   = sum(PQM[c]   * QM[c]   if QM[c]  <> 0 on c) 			  if QM  	<> 0

## Total quantity of imported commodities
	    QM   = sum( 		  QM[c]   if QM[c]  <> 0 on c)  		  if QM  	<> 0

# Aggregation of imports and domestic margins per use
## Aggregate market price for margins received on domestically produced commodities
PMGRD * MGRD = sum(PMGRD[c] * MGRD[c] if MGRD[c] <> 0 on c) 		  if MGRD 	<> 0

## Total quantity of margins received on domestically produced commodities
	    MGRD = sum(    		  MGRD[c] if MGRD[c] <> 0 on c) 		  if MGRD 	<> 0

## Aggregate market price for margins received on imported commodities
PMGRM * MGRM = sum(PMGRM[c] * MGRM[c] if MGRM[c] <> 0 on c) 		  if MGRM 	<> 0

## Total quantity of margins received on imported commodities
	    MGRM = sum(		   	  MGRM[c] if MGRM[c] <> 0 on c) 		  if MGRM 	<> 0

# Aggregation of imports and domestic intermediate consumption per use
## Aggregate market price on domestically produced commodities for intermediate consumption use
PCID  * CID  = sum(PCID[c]  * CID[c]  if CID[c]  <> 0 on c) 		  if  CID 	<> 0

## Total quantity of domestically produced commodities for intermediate consumption use
	    CID  = sum( 		  CID[c]  if CID[c]  <> 0 on c) 		  if  CID 	<> 0

## Aggregate market price on imported commodities for intermediate consumption use
PCIM  * CIM  = sum(PCIM[c]  * CIM[c]  if CIM[c]  <> 0 on c) 		  if  CIM 	<> 0

## Total quantity of imported commodities for intermediate consumption use
	    CIM  = sum( 		  CIM[c]  if CIM[c]  <> 0 on c) 		  if  CIM 	<> 0

# Aggregation of imports and domestic households consumption per use

## Aggregate market price on domestically produced commodities for households final consumption expenditure
PCHD  * CHD  = sum(PCHD[c]  * CHD[c]  if CHD[c]  <> 0 on c) 		  if  CHD 	<> 0

## Total quantity of domestically produced commodities for hoexpenditureholds final consumption expenditure
	    CHD  = sum( 	      CHD[c]  if CHD[c]  <> 0 on c) 		  if  CHD 	<> 0

## Aggregate market price on imported commodities for households final consumption expenditure
PCHM  * CHM  = sum(PCHM[c]  * CHM[c]  if CHM[c]  <> 0 on c) 		  if  CHM 	<> 0

## Total quantity of imported commodities for households final consumption expenditure
	    CHM  = sum( 		  CHM[c]  if CHM[c]  <> 0 on c) 		  if  CHM 	<> 0

# Aggregation of imports and domestic government consumption per use
## Aggregate market price on domestically produced commodities for government final consumption expenditure
PGD   * GD   = sum(PGD[c]   * GD[c]   if GD[c]   <> 0 on c) 		  if   GD 	<> 0

## Total quantity of domestically produced commodities for government final consumption expenditure
	    GD   = sum( 		  GD[c]   if GD[c]   <> 0 on c) 		  if   GD 	<> 0

## Aggregate market price on imported commodities for government final consumption expenditure
PGM   * GM   = sum(PGM[c]   * GM[c]   if GM[c]   <> 0 on c) 		  if   GM 	<> 0

## Total quantity of imported commodities for government final consumption expenditure
	    GM   = sum( 		  GM[c]   if GM[c]   <> 0 on c) 		  if   GM 	<> 0

# Aggregation of imports and domestic investment per use

## Aggregate market price on domestically produced commodities for investment use
PID   * ID   = sum(PID[c]   * ID[c]   if ID[c]   <> 0 on c) 		  if   ID 	<> 0

## Total quantity of domestically produced commodities for investment use
	    ID   = sum( 		  ID[c]   if ID[c]   <> 0 on c) 		  if   ID 	<> 0

## Aggregate market price on imported produced commodities for investment use
PIM   * IM   = sum(PIM[c]   * IM[c]   if IM[c]   <> 0 on c) 		  if   IM 	<> 0

## Total quantity of imported commodities for investment use
	    IM   = sum( 		  IM[c]   if IM[c]   <> 0 on c) 		  if   IM 	<> 0

# Aggregation of imports and domestic exports per use
## Aggregate market price on domestically produced commodities for exports use
PXD   * XD   = sum(PXD[c]   * XD[c]   if XD[c]   <> 0 on c) 		  if   XD 	<> 0

## Total quantity of domestically produced commodities for exports use
	    XD   = sum( 		  XD[c]   if XD[c]   <> 0 on c) 		  if   XD 	<> 0

## Aggregate market price on imported commodities for exports use
PXM   * XM   = sum(PXM[c]   * XM[c]   if XM[c]   <> 0 on c) 		  if   XM 	<> 0

## Total quantity of imported commodities for exports use
	    XM   = sum( 		  XM[c]   if XM[c]   <> 0 on c) 		  if   XM 	<> 0

# Aggregation of imports and domestic change in inventories per use
## Aggregate market price on domestically produced commodities for change in inventories use
PDSD  * DSD  = sum(PDSD[c]  * DSD[c] if DSD[c]   <> 0 on c) 		  if  DSD 	<> 0

## Total quantity of domestically produced commodities for change in inventories use
	    DSD  = sum( 		  DSD[c] if DSD[c]   <> 0 on c) 		  if  DSD 	<> 0

## Aggregate market price on imported commodities for change in inventories use
PDSM  * DSM  = sum(PDSM[c]  * DSM[c] if DSM[c]   <> 0 on c) 		  if  DSM 	<> 0

## Total quantity of imported commodities for change in inventories use
	    DSM  = sum( 		  DSM[c] if DSM[c]   <> 0 on c) 		  if  DSM 	<> 0

# Aggregation per use of total production 					(value & volume)
## Aggregate market price of commodities
PQ 	  * Q    = PQD   * QD   + PQM   * QM   							  if Q    	<> 0

## Total quantity of commodities
	    Q    = 	       QD   + 	      QM   							  if Q    	<> 0

# Aggregation per use of margins 								(value & volume)
## Aggregate market price of margins on commodities
PMGR  * MGR  = PMGRD * MGRD + PMGRM * MGRM 							  if MGR  	<> 0

## Total quantity of margins on commodities
	    MGR  = 		   MGRD + 		  MGRM 							  if MGR  	<> 0

# Aggregation per use of intermediate consumptions 			(value & volume)
## Aggregate market price of commodities for intermediate consumption use
PCI   * CI   = PCID  * CID  + PCIM  * CIM  							  if CI   	<> 0

## Total quantity of commodities for intermediate consumption use
	    CI   = 		   CID  + 	      CIM  							  if CI   	<> 0

# Aggregation per use of households consumption 				(value & volume)
## Aggregate market price of commodities for household final consumption expenditure
PCH   * CH   = PCHD  * CHD  + PCHM  * CHM  							  if CH   	<> 0

## Total quantity of commodities for household final consumption expenditure
	    CH   = 		   CHD  + 	      CHM  							  if CH   	<> 0

# Aggregation per use of government consumption 				(value & volume)
## Aggregate market price of commodities for government final consumption expenditure
PG    * G    = PGD   * GD   + PGM   * GM   							  if G    	<> 0

## Total quantity of commodities for government final consumption expenditure
	    G    = 	       GD   + 	      GM   							  if G    	<> 0

# Aggregation per use of investment 							(value & volume)
# Aggregate market price of commodities for investment use
PI    * I    = PID   * ID   + PIM   * IM   							  if I    	<> 0

## Total quantity of commodities for investment use
	    I    = 	       ID   + 	      IM   							  if I    	<> 0

# Aggregation per use of exports 								(value & volume)
# Aggregate market price of commodities for exports use
PX    * X    = PXD   * XD   + PXM   * XM   							 if X    	<> 0

## Total quantity of commodities for exports use
	    X    = 	       XD   + 	      XM   							 if X    	<> 0

# Aggregation of total change in inventories 						(value & volume)
# Aggregate market price of commodities for change in inventories use
PDS   * DS   = PDSD  * DSD  + PDSM  * DSM  							 if DS   	<> 0

## Total quantity of commodities for change in inventories use
	    DS   = 		   DSD  + 	   	  DSM  							 if DS   	<> 0


### Supply side

# Margins paid on domestic and imported commodity $c$ 		(value & volume)
## price for margins paid on domestically produced commodity $c$
PMGPD[c] * MGPD[c] = sum(PMGPD[cc, c] * MGPD[cc, c] if MGPD[cc, c] <> 0 on cc) 	   if MGPD[c]  	 <> 0

## Quantity of margins paid on domestically produced commodity $c$
		   MGPD[c] = sum(			    MGPD[cc, c] if MGPD[cc, c] <> 0 on cc) 	   if MGPD[c]  	 <> 0

## price for margins paid on imported commodity $c$
PMGPM[c] * MGPM[c] = sum(PMGPM[cc, c] * MGPM[cc, c] if MGPM[cc, c] <> 0 on cc) 	   if MGPM[c]  	 <> 0

## Quantity of margins paid on imported commodity $c$
		   MGPM[c] = sum(				MGPM[cc, c] if MGPM[cc, c] <> 0 on cc) 	   if MGPM[c]  	 <> 0

##  Domestic production of commodity $c$ at basic price:
##  The price can not be defined as an index because it is already defined as a function of the production price in the price block.

## Quantity of production of commodity $c$ expressed at domestic production price
YQ[c] * PYQ[c] 		 + NTAXPD_VAL[c]  + PMGPD[c] * MGPD[c] = PQD[c] * QD[c]    	   if YQ[c]      <> 0

## Same variable calculated from values & volumes. For verification. (NO COMMENT ?)

PYQbis[c] * YQ[c] 	 + NTAXPD_VAL[c]  + PMGPD[c] * MGPD[c] = PQD[c] * QD[c]    	   if YQ[c]      <> 0
			YQbis[c] + NTAXPD[c] 	  + 		   MGPD[c] = 		  QD[c]    	   if YQbis[c]   <> 0

##  Imported production of commodity $c$ at basic price:
##  The price can not be defined as an index because it is already defined as a function of the production price in the price block.

## Quantity of imports of commodity $c$ expressed at imports price
M[c] * PM[c] + NTAXPM_VAL[c] + PMGPM[c] * MGPM[c] = PQM[c] * QM[c] 			   	   if M[c] 	     <> 0

## Same variable calculated from volumes. For verification. (NO COMMENT?)

PMbis[c] * M[c]      + NTAXPM_VAL[c]  + PMGPM[c] * MGPM[c] = PQM[c] * QM[c] 	   if M[c]       <> 0
		   Mbis[c]   + NTAXPM[c] 	  + 		   MGPM[c] = 		  QM[c] 	   if Mbis[c]    <> 0

## Price of margins paid to commodity $cc$ on commodity $c$
PMGP[cc, c] * MGP[cc, c] = PMGPD[cc, c] * MGPD[cc, c] + PMGPM[cc, c] * MGPM[cc, c] if MGP[cc, c] <> 0

# Margins paid to commodity $cc$ on commodity $c$ (value & volume):
## Margins paid to commodity $cc$ on commodity $c$ expressed at margin paid price
			  MGP[cc, c] = 				  MGPD[cc, c] + 			   MGPM[cc, c] if MGP[cc, c] <> 0

## Margins recieved by commodity $cc$ (value & volume):
## Price of margins received by commodity $cc$
PMGR[cc] * MGR[cc]  = sum(PMGP[cc, c] * MGP[cc, c] 	if MGP[cc, c] <> 0 on c) 	   if MGR[cc]    <> 0

## Margins received by commodity $cc$ expressed at margin received price
           MGR[cc]  = sum(    	 	    MGP[cc, c] 	if MGP[cc, c] <> 0 on c) 	   if MGR[cc]    <> 0

           ## Same variable calculated from values & volumes. For verification. (NO COMMENT ?)

PMGRbis[c] * MGRbis[c] = PMGRD[c] * MGRD[c] + PMGRM[c] * MGRM[c] 				   if MGRbis[c]  <> 0
			 MGRbis[c] = 		    MGRD[c] + 			 MGRM[c] 				   if MGRbis[c]  <> 0

## Remark about margins:
## The margins paid $MGPD_{cc, c}$ and $MGPM_{cc, c}$ are defined with behavorial equations. They follow $YQ_{c}$ and $M_{c}$ (more or less proportionnally depending on the possibility of substitutions between margins). See producer block. The margins paid are then agregated to define the margins recieved $MGR_{cc}$. The latter is then disagrated between the domestic and imported margins recieved ($MGRD_{c}$ and $MGRM_{c}$). See producer block.

# Aggregate margins paid on the domesticaly produced commodity (value & volume):
## Aggregate price of margins paid on domestically produced commodity $c$
PMGPD   * MGPD 		= sum(PMGPD[c]   * MGPD[c] 	 	if MGPD[c]	  <> 0 on c) 		if MGPD 	 <> 0

## Quantity of margins paid on domestically produced commodities
          MGPD 		= sum(             MGPD[c] 	 	if MGPD[c]    <> 0 on c) 		if MGPD 	 <> 0

# Aggregate margins paid on the imported commodity (value & volume):
## Aggregate price of margins paid on imported commodities
PMGPM   * MGPM 		= sum(PMGPM[c]   * MGPM[c] 	 	if MGPM[c]    <> 0 on c) 		if MGPM 	 <> 0

## Quantity of margins paid on imported commodities
          MGPM 		= sum(             MGPM[c] 	 	if MGPM[c]    <> 0 on c) 		if MGPM 	 <> 0

#  Aggregate domestic production of commodity $c$ at basic price:
## Aggregate basic price of domestically produced commodities
PYQ     * YQ 		= sum(PYQ[c]     * YQ[c] 		if YQ[c] 	  <> 0 on c) 		if YQ 		 <> 0

## Total domestic production expressed at basic price
	      YQ 		= sum(		       YQ[c] 		if YQ[c] 	  <> 0 on c) 		if YQ 		 <> 0

# Aggregate imports of commodity $c$ at basic price:
## Aggregate basic price of imported commodities
PM      * M 		= sum(PM[c]      * M[c]    	 	if M[c] 	  <> 0 on c) 		if M 		 <> 0

## Total imported commodities expressed at basic price
	      M 		= sum(	           M[c] 		if M[c] 	  <> 0 on c) 		if M 		 <> 0

## Domestic and imported intermediary raw material consumption of sector $s$ (value & volume):

## Aggregate basic price of domestically produced commmodities for intermediate consumption use by the sector $s$
PCID[s] * CID[s] 	= sum(PCID[c, s] * CID[c, s] 	if CID[c, s]  <> 0 on c) 		if CID[s]    <> 0

## Quantity of domestically produced commmodities for intermediate consumption use by the sector $s$ expressed at basic price
		  CID[s] 	= sum(			   CID[c, s] 	if CID[c, s]  <> 0 on c) 		if CID[s] 	 <> 0

## Aggregate basic price of imported commmodities for intermediate consumption use by the sector $s$
PCIM[s] * CIM[s] 	= sum(PCIM[c, s] * CIM[c, s] 	if CIM[c, s]  <> 0 on c) 		if CIM[s] 	 <> 0

## Quantity of imported commmodities for intermediate consumption use by the sector $s$ expressed at basic price
		  CIM[s] 	= sum(			   CIM[c, s] 	if CIM[c, s]  <> 0 on c) 		if CIM[s] 	 <> 0

# Intermediary raw material consumption of sector $s$ (value & volume):
## Aggregate basic price of commmodities for intermediate consumption use by the sector $s$
PCI[s]  * CI[s]     = PCID[s] * CID[s] + PCIM[s] * CIM[s] 					 		if CI[s]  	 <> 0

## Quantity of commmodities for intermediate consumption use by the sector $s$ expressed at basic price
		  CI[s]     = 		    CID[s] + 		   CIM[s] 					 		if CI[s]  	 <> 0

## Intermediary raw material from sector aggregation (value & volume) : NO COMMENT

PCIbis  * CIbis     = sum(PCI[s]     * CI[s] 		if CI[s] 	  <> 0 on s)    	if CI 	     <> 0
		  CIbis     = sum(  	       CI[s] 		if CI[s] 	  <> 0 on s)        if CI 	     <> 0

## Domestic and imported investment of sector $s$ (value & volume):
## Aggregate investment price of domestically produced commmodities for investment use by the sector $s$
PID[s]  * ID[s]     = sum(PID[c, s]  * ID[c, s] 	if ID[c, s]   <> 0 on c) 	    if ID[s]  	 <> 0

## Quantity of domestically produced commmodities for investment use by the sector $s$ expressed at investment price of domestically produced commodities
		  ID[s]     = sum( 			   ID[c, s] 	if ID[c, s]   <> 0 on c) 	 	if ID[s]  	 <> 0

## Aggregate investment price of imported commmodities for investment use by the sector $s$
PIM[s]  * IM[s]     = sum(PIM[c, s]  * IM[c, s] 	if IM[c, s]   <> 0 on c) 	 	if IM[s]  	 <> 0

## Quantity of imported commmodities for investment use by the sector $s$ expressed at investment price of imported commodities
		  IM[s]     = sum(			   IM[c, s] 	if IM[c, s]   <> 0 on c) 	 	if IM[s]  	 <> 0

# Investment of sector $s$ (value & volume):
## Aggregate investment price of commmodities for investment use by the sector $s$
PI[s]   * I[s]      = PID[s] * ID[s] + PIM[s] * IM[s] 						   	 	if I[s]   	 <> 0

## Total quantity of commodities for investment use by the sector $s$ expressed at investment price
	      I[s]      = 		   ID[s] + 		    IM[s] 						   	 	if I[s]   	 <> 0

## Investment from sector agregation (value & volume) (summed directly on the aggregate per product $c$). For verification: (NO COMMENT ?)

PIbis   * Ibis      = sum(PI[s]      * I[s] 		if I[s]	  	  <> 0 on s) 		if I 		 <> 0
		  Ibis      = sum(		   	   I[s] 		if I[s] 	  <> 0 on s) 		if I 		 <> 0

# Production of sector $s$ (value & volume):
# A mettre probablement en bis! Car defini dans producteur
# Mettre VERIF sur le PRIX !!!!! PYbis[s]

#PYbis[s] * Y[s] = sum(PY[c, s] * Y[c, s] if Y[c, s] <> 0 on c) if Y[s] <> 0

## Quantity of commodities produced from the sector $s$ expressed at the production price
		Y[s]        = sum(Y[c, s] 			 	    if Y[c, s]    <> 0 on c) 		if Y[s] 	 <> 0

# Aggregate total production (value & volume):
## Aggregate production price for all sectors
PY * Y              = sum(PY[s] * Y[s] 				if Y[s] 	  <> 0 on s) 		if Y 		 <> 0

## Total quantity of production expressed at production price
	 Y              = sum( 		  Y[s] 			    if Y[s] 	  <> 0 on s) 		if Y 		 <> 0

# Value-added of sector s (value & volume):
# It may not be possible to calculate the price of the value-added (VA) since the VA may be equal to zero at the sector level. For this reason, we define the VA in value (VA_VAL). To avoid any bug, disable the equation of PVA[s]. Moreover there should be an equation for the VA even if it is equal to zero at the base year.

## Added-value of sector $s$ expressed in value
VA_VAL[s] = PY[s] * Y[s] - PCI[s] * CI[s]

## Added-value of sector $s$ expressed in volume
VA[s] 	  = 		Y[s] - 			CI[s]

#PVA[s] * VA[s] = VA_VAL[s]

# Aggregate value-added (value & volume):
## Aggregate value-added price
PVA * VA = sum(VA_VAL[s] on s)

## Total value-added expressed at value-added price
	  VA = sum(VA[s]     on s)

## Aggregate gross wages paid by sector $s$ including employees (but not employers)' social contribution

PWAGES * WAGES = sum(PWAGES[s] * WAGES[s] if WAGES[s] <> 0 on s) if WAGES <> 0
		 WAGES = sum(			 WAGES[s] if WAGES[s] <> 0 on s) if WAGES <> 0

# Gross operating surplus of sector $s$ (value & volume)
# Same remark than for VA
## Gross operating surplus of sector $s$ expressed in value
GOS_VAL[s] = VA_VAL[s] - PWAGES[s] * WAGES[s] - PRSSC[s] * RSSC[s] - NTAXI_VAL[s]

## Gross operating surplus of sector $s$ expressed in volume
GOS[s] 	   = VA[s] 	   - 			 WAGES[s] - 		   RSSC[s] - NTAXI[s]


## Aggregate price of Gross Operating Surplus
## The exact definition of the GOS generally exclude tax on profit. We do not do it here for simplicity and assume that $NTAXI_{s}$ includes all net taxes on capital (i.e. tax on production and profits). This should be taken into account if one wants to use the $GOS$ as a basis for the profit taxes.
PGOS * GOS = sum(GOS_VAL[s] on s)

## Total Gross Operating Surplus
	   GOS = sum(GOS[s] 	on s)


# Same remark than for VA
## Net operating surplus of sector $s$ expressed in value
NOS_VAL[s] = GOS_VAL[s] - PK[s]{-1} 				  * Rdep[s] * F[K, s]{-1}

## Net operating surplus of sector $s$ expressed in volume
NOS[s] 	   = GOS[s]     - @elem(PK[s]{-1}, %baseyear) * Rdep[s] * F[K, s]{-1}

## Aggregate net operating surplus price
PNOS * NOS = sum(NOS_VAL[s] on s)

## Total Net Operating Surplus
	   NOS = sum(NOS[s] 	on s)

### GDP calculated according to various approaches
# GDP: Expenditure approach
# Calculated as the sum of the different components in the final uses of goods & services

## Total GDP price from expenditure approach
PGDP 	* GDP 	 = PCH 	  * CH	  + PG 	  * G 	 + PI 	 * I 	+ PX 	* X    + PDS    * DS 	- PM    * M

## Total GDP from expenditure approach
		  GDP 	 = 	     	CH 	  + 	    G 	 + 	  	   I 	+ 	 	  X    + 	   	  DS 	- 	 	  M

## Total GDP price of commodity $c$ from expenditure approach
##  GDP price of commodity $c$ from expenditure approach
PGDP[c] * GDP[c] = PCH[c] * CH[c] + PG[c] * G[c] + PI[c] * I[c] + PX[c] * X[c] + PDS[c] * DS[c] - PM[c] * M[c] 		if GDP[c] <> 0

##  GDP of commodity $c$ from expenditure approach
		  GDP[c] = 			CH[c] + 		G[c] + 		   I[c] + 		  X[c] + 		  DS[c] - 		  M[c]		if GDP[c] <> 0

## Agregated GDP (value & volume) calculated from the GDP per using commodity (NO COMMMENT ?)

PGDPbis * GDPbis = sum(PGDP[c] * GDP[c] on c)
		  GDPbis = sum(		  	 GDP[c] on c)

## Total GDP from production approach
## Calculated as the sum of the gross value added plus the total net taxes
PGDPter * GDPter = PVA * VA + PNTAXP * NTAXP

## GDP price from production approach
		  GDPter = 		 VA + 		   NTAXP


## Total GDP from income approach
## Calculated as the sum of all the economic incomes (from labor and capital) corrected by the social & taxes transfers
PGDP4 	* GDP4   = PGOS * GOS + PWAGES * WAGES + PRSSC * RSSC + NTAXI_VAL + PNTAXP * NTAXP
##  GDP price from income approach
		  GDP4   = 		  GOS + 		 WAGES + 		 RSSC + NTAXI 	  + 		 NTAXP
