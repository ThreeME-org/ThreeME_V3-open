
#####  Taxes & Prices

### This file provides the equations defining with more detailed taxes and prices.

#### Taxes on commodities
 # In the current version of the compiler, the  @over are not taken account and not read.
 # In the current version of the compiler, the  @over are compiled in the glossary instead of the variables in front of which the @over is placed.

### Average tax rates on commodities

##! Net taxes on domestically produced commodity $c$ expressed in value
@over NTAXCD_VAL[c] = PVATD[c] * VATD[c] + POTHCTD[c] * OTHCTD[c] + PSUBCD[c] * SUBCD[c]        if NTAXCD_VAL[c] <> 0

##! Net taxes on domestically produced commodity $c$
@over NTAXCD[c]     =            VATD[c] +              OTHCTD[c] +             SUBCD[c]        if NTAXCD[c]     <> 0

## This is not defined by simply assuming a constant rate over the production value : $NTAXCD_VAL[c] = RNTAXCD[c]*PYQ[c]*YQ[c]$. Different taxes are now defined depending on their basis. \\

##! Average tax rate on domestically produced commodity $c$
RNTAXCD[c] = NTAXCD_VAL[c] / (PYQ[c] * YQ[c])     if YQ[c] <> 0

## This rate become now endogenous by inverting the equation of $NTAXCD_VAL[c]$. This specification ensure the equality accountancy between the various GDP definition. \\

##! Net taxes on imported commodity $c$ expressed in value
@over NTAXCM_VAL[c] = PVATM[c] * VATM[c] + POTHCTM[c] * OTHCTM[c] + PSUBCM[c] * SUBCM[c]        if NTAXCM_VAL[c] <> 0

##! Net taxes on imported commodity $c$
@over NTAXCM[c]     =            VATM[c] +              OTHCTM[c] +             SUBCM[c]        if NTAXCM[c]     <> 0

##! Average tax rate on imported commodity $c$
RNTAXCM[c] = NTAXCM_VAL[c] / (PM[c]  * M[c] )   if M[c] <> 0

### Value-added tax

##! Price of the value-added tax on domestically produced commodity $c$
PVATD[c]   * VATD[c]   =       RVATD[c] * PCHD[c]     * CHD[c] / (1 + RVATD[c])                 if VATD[c]       <> 0

##! Value-added tax on domestically produced commodity $c$
VATD[c]                = @elem(RVATD[c],%baseyear)    * CHD[c] / (1+@elem(RVATD[c], %baseyear)) if VATD[c]       <> 0

##! Price of the value-added tax on imported commodity $c$
PVATM[c]   * VATM[c]   =       RVATM[c]  * PCHM[c]    * CHM[c] / (1 + RVATM[c])                 if VATM[c]       <> 0

##! Value-added tax on imported commodity $c$
             VATM[c]   = @elem(RVATM[c],%baseyear)    * CHM[c] / (1+@elem(RVATM[c], %baseyear)) if VATM[c]       <> 0

### Other taxes on commodities (value & volume)

##! Price of the other taxes on domestically produced commodity $c$
POTHCTD[c] * OTHCTD[c] =       ROTHCTD[c]   * PYQ[c]  * YQ[c]                                   if OTHCTD[c]     <> 0

##! Other taxes on domestically produced commodity $c$
             OTHCTD[c] = @elem(ROTHCTD[c], %baseyear) * YQ[c]                                   if OTHCTD[c]     <> 0

##! Price other taxes on imported commodity $c$
POTHCTM[c] * OTHCTM[c] =       ROTHCTM[c] * PM[c]     * M[c]                                    if OTHCTM[c]     <> 0

##! Other taxes on imported commodity $c$
             OTHCTM[c] = @elem(ROTHCTM[c], %baseyear) * M[c]                                    if OTHCTM[c]     <> 0

### Subsidies on commodities (value & volume)

##!Price of subsidies on domestically produced commodity $c$
PSUBCD[c]  * SUBCD[c]  =       RSUBCD[c]              * YQ[c]                                   if SUBCD[c]      <> 0

##! Subsidies on domestically produced commodity $c$
             SUBCD[c]  = @elem(RSUBCD[c], %baseyear)  * YQ[c]                                   if SUBCD[c]      <> 0

## We assume that subsidies are on the volume of production. \\

##! Price of the subsidies on imported commodity $c$
PSUBCM[c]  *  SUBCM[c] =       RSUBCM[c]              * M[c]                                    if SUBCM[c]      <> 0

##! Subsidies on imported commodity $c$
              SUBCM[c] = @elem(RSUBCM[c], %baseyear)  * M[c]                                    if SUBCM[c]      <> 0


#######################################

#### Prices of commodities
##  Prices should be defined in coherence with the above specification of taxes. \\

##! Selling price before VAT for domestically produced commodity $c$
PYQSBVAT[c] * YQSBVAT[c] = PYQ[c] * YQ[c] + POTHCTD[c] * OTHCTD[c] + PSUBCD[c] * SUBCD[c] + PMGPD[c] * MGPD[c] if YQSBVAT[c] <> 0

##! Production of commodity $c$ expressed at market price before VAT
d(log(YQSBVAT[c])) = d(log(YQ[c]))	                                                                           if YQSBVAT[c] <> 0

##! Selling price before VAT for imported commodity $c$ (Value & volume)
PMSBVAT[c]  * MSBVAT[c]  = PM[c]  * M[c]  + POTHCTM[c] * OTHCTM[c] + PSUBCM[c] * SUBCM[c] + PMGPM[c] * MGPM[c] if MSBVAT[c]  <> 0

##! Imports of commodity $c$ expressed at market price before VAT
d(log(MSBVAT[c] )) = d(log(M[c] ))                                                                             if MSBVAT[c]  <> 0

##! Price of domestically produced margins supplied by commodity $c$
@over PMGSD[c]   = PYQSBVAT[c]                                                     if MGSD[c]   <> 0

##! Price of imported margins supplied by commodity $c$
@over PMGSM[c]   = PMSBVAT[c]                                                      if MGSM[c]   <> 0

##! Price of intermediary raw material consumption domestically produced $c$ of sector $s$
@over PCID[c, s] = PYQSBVAT[c]                                                     if CID[c, s] <> 0

##! Price of the imported intermediary raw material consumption $c$ of sector $s$
@over PCIM[c, s] = PMSBVAT[c]                                                      if CIM[c, s] <> 0

##! Price for households final consumption of domestically produced commodity $c$
@over PCHD[c]    = PYQSBVAT[c] * (1 + RVATD[c]) / (1 + @elem(RVATD[c], %baseyear)) if CHD[c]    <> 0

##! Price for households final consumption of Imported commodity $c$
@over PCHM[c]    = PMSBVAT[c]  * (1 + RVATM[c]) / (1 + @elem(RVATM[c], %baseyear)) if CHM[c]    <> 0

##! Market price for government final consumption of domestically produced commodity $c$
@over PGD[c]     = PYQSBVAT[c]                                                     if GD[c]     <> 0

##! Market price for government final consumption of imported commodity $c$
@over PGM[c]     = PMSBVAT[c]                                                      if GM[c]     <> 0

##! Market price for investment of domestically produced commodity $c$
@over PID[c, s]  = PYQSBVAT[c]                                                     if ID[c, s]  <> 0

##! Market price for investment of imported commodity $c$
@over PIM[c, s]  = PMSBVAT[c]                                                      if IM[c, s]  <> 0

##! Market price for exports of domestically produced commodity $c$
@over PXD[c]     = PYQSBVAT[c]                                                     if XD[c]     <> 0

##! Market price for exports of imported commodity $c$
@over PXM[c]     = PMSBVAT[c]                                                      if XM[c]     <> 0

##! Market price for change in inventories of domestically produced commodity $c$
@over PDSD[c]    = PYQSBVAT[c]                                                     if DSD[c]    <> 0

##! Market price for change in inventories of imported commodity $c$
@over PDSM[c]    = PMSBVAT[c]                                                      if DSM[c]    <> 0

##################################
### Aggregation of taxes

##! Aggregate market price for Value-added Tax on domestically produced households final consumption
PVATD   * VATD = sum(PVATD[c] *     VATD[c]   if VATD[c]   <> 0 on c)  if VATD   <> 0
##! Aggregate Value-added Tax on domestically produced households final consumption
          VATD = sum(               VATD[c]   if VATD[c]   <> 0 on c)  if VATD   <> 0

##! Aggregate market price for Other product Tax on domestically produced households final consumption
POTHCTD * OTHCTD = sum(POTHCTD[c] * OTHCTD[c] if OTHCTD[c] <> 0 on c)  if OTHCTD <> 0

##! Aggregate Other product Tax on domestically produced households final consumption
          OTHCTD = sum(             OTHCTD[c] if OTHCTD[c] <> 0 on c)  if OTHCTD <> 0

##! Aggregate market price for subsidies on domestically produced commodities
PSUBCD  * SUBCD  = sum(PSUBCD[c]  * SUBCD[c]  if SUBCD[c]  <> 0 on c)  if SUBCD  <> 0

##! Aggregate subsidies on domestically produced commodities
          SUBCD  = sum(             SUBCD[c]  if SUBCD[c]  <> 0 on c)  if SUBCD  <> 0

##! Aggregate market price for Value-added Tax on imported households final consumption
PVATM   * VATM   = sum(PVATM[c]   * VATM[c]   if VATM[c]   <> 0 on c)  if VATM   <> 0

##! Aggregate Value-added Tax on imported households final consumption
          VATM   = sum(             VATM[c]   if VATM[c]   <> 0 on c)  if VATM   <> 0

##! Aggregate market price for Other product Tax on imported commododities
POTHCTM * OTHCTM = sum(POTHCTM[c] * OTHCTM[c] if OTHCTM[c] <> 0 on c)  if OTHCTM <> 0

##! Aggregate Other product Tax on imported commododities
          OTHCTM = sum(             OTHCTM[c] if OTHCTM[c] <> 0 on c)  if OTHCTM <> 0

##! Aggregate market price for subsidies on imported commododities
PSUBCM  * SUBCM  = sum(PSUBCM[c]  * SUBCM[c]  if SUBCM[c]  <> 0 on c)  if SUBCM  <> 0

##! Aggregate subsidies on imported commodities
          SUBCM  = sum(             SUBCM[c]  if SUBCM[c]  <> 0 on c)  if SUBCM  <> 0

##! Aggregate market price for Value-added Tax on total households final consumption
PVAT    * VAT    = PVATD   * VATD   + PVATM  * VATM                    if VAT    <> 0

##! Aggregate Value-added Tax on total households final consumption
          VAT    =           VATD   +          VATM                    if VAT    <> 0

##! Aggregate market price for other product Tax
POTHCT  * OTHCT  = POTHCTD * OTHCTD + POTHCTM * OTHCTM                 if OTHCT  <> 0

##! Aggregate Other product Tax
      	  OTHCT  =           OTHCTD +           OTHCTM                 if OTHCT  <> 0

##! Aggregate market price for subsidies on commodities
PSUBC   * SUBC   = PSUBCD  * SUBCD  + PSUBCM  * SUBCM                  if SUBC   <> 0

##! Aggregate subsidies on commodities
          SUBC   =           SUBCD  +           SUBCM                  if SUBC   <> 0

##################################
### Average tax rates

##! Average VAT rate on domestically produced commodity
RVATD = (PVATD * VATD) / (PCHD * CHD - PVATD   * VATD)

##! Average rate of other taxes on domestically produced commodity
ROTHCTD = POTHCTD * OTHCTD / (PYQ * YQ) 

##! Average rate of subsidies on domestically produced commodity
RSUBCD = PSUBCD  * SUBCD / YQ

##! Average VAT rate on imported commodity
RVATM = (PVATM * VATM) / (PCHM * CHM - PVATM * VATM)

##! Average rate of other taxes on imported commodity
ROTHCTM = POTHCTM * OTHCTM / (PM * M) 

##! Average rate of subsidies on imported commodity
RSUBCM = PSUBCM  * SUBCM / M        

##! Average VAT rate
RVAT = (PVAT * VAT) / (PCH * CH - PVAT   * VAT)

##! Average rate of other taxes on commodity
ROTHCT = POTHCT * OTHCT / (PYQ * YQ + PM * M) 

##! Average rate of subsidies on commodity 
RSUBC = PSUBC  * SUBC / (YQ  + M)        
