########################################################
#########         PRICES         #####################
########################################################
### This file provides the equations defining the prices.

### Domestic production price of commodity c:
PYQ[c] * YQ[c] = sum(PY[s] * Y[c, s] if Y[c, s] <> 0 on s) if YQ[c] <> 0

### Notional production price of sector s 

PY_n[s] = CU_n[s] * (1 + MARKUP[s])

## Notional mark-up

## Notional mark-up
d(log(1+MARKUP_n[s])) = RHO_MARKUP_Y * d(log(CUR[s])) if Y[s] > 0

# Notice that this is strictly equivalent to:
# d(log(1+MARKUP_n[s])) = RHO_MARKUP_Y * (d(log(Y[s])) - d(log(YCAP[s]{-1}))) if Y[s] > 0
# Which is quite close to the specification previously used:
# d(log(1+MARKUP_n[s])) = RHO_MARKUP_Y * (d(log(Y[s])) - d(log(Y[s]{-1}))) if Y[s] > 0


d(log(YCAP[s])) = sum(phi[f, s]{-1} * d(log(F[f, s]*PROG[f, s])) if F[f, s] <> 0 on f) + ADJUST_YCAP_Y[s] * (log(Y[s]{-1}) - log(YCAP[s]{-1} * @elem(CUR[s], %baseyear))) if YCAP[s] <> 0

# Capacity Utilisation ratio
CUR[s] = Y[s] / YCAP[s]

# Average mark-up of activities per commodities
(1 + MARKUP[c]) = PYQ[c] * YQ[c] / (sum(CU[s] * Y[c, s] if Y[c, s] <> 0 on s)) if YQ[c] <> 0

## Notional unit cost of production in sector s
## To define the notional price, it is preferable to use the notional unit cost of production instead of the effective one. This lead to a more stable dynamic and gives a better representation of anticipation. 
CU_n[s] * Y[s] = sum(C[f, s] * F_n[f, s] on f) + NTAXI_VAL[s]

## Unit cost of production in sector s
CU[s] * Y[s] = sum(C[f, s] * F[f, s] on f) + NTAXI_VAL[s]

## Labor cost in sector s
C[L, s] =  W[s] * (1 + RRSSC[s])

### Capital cost in sector s
### It is preferable to calculate the user cost of capital based on the price of capital rather than on the price of investment. Indeed the price of the average capital installed is lower than the one of investment because of inflation. Using the price of investment tend to over estimate the cost of capital because it assumes that the debt contracted to finance past investments is indexed on inflation which is not the case in reality. 

C[K, s] = PK[s] * (Rdep[s] + r[s])
 
### Price of capital in sector s
### The price of capital is calibrated by rewriting this equation in the long run. It is always smaller than 1 because it is calibrated as follows: PK[s] = PI[s]*(Rdep[s]+GR_REAL)*(1+GR_PRICES)/(Rdep[s]-1+(1+GR_REAL)*(1+GR_PRICES))

PK[s] * F[K, s] = (1 - Rdep[s]) * PK[s]{-1} * F[K, s]{-1} + PI[s] * I[s]

### Energy costs in sector s
### In first approximation the cost of energy correspond to the energy price. However if the producer is forward looking, she will integrate the anticipation of price increase in it definition of the user cost of energy. In this case the specification becomes ....
C[E, s] = PE[s]

### Materal costs in sector s
C[MAT, s] = PMAT[s] 

### Agregate costs for capital, labor, energy and material
C[K] * F[K] = sum(C[K, s] * F[K, s] on s)
C[L] * F[L] = sum(C[L, s] * F[L, s] on s)
C[E] * F[E] = sum(C[E, s] * F[E, s] on s)
C[MAT] * F[MAT] = sum(C[MAT, s] * F[MAT, s] on s)



### Volume and price of the bruto wages paid by sector s including employees (but not employers)' social contribution
### To derive the volume, we assume that the price is the consumer price 

WAGES[s] * PWAGES[s] = W[s] * F[L, s]

PWAGES[s] = P

### Price of commodity c
### For CH (households'consumption); G (public spendings); X (exports)
P|V|[c] * |V|[c] = P|V|D[c] * |V|D[c] + P|V|M[c] * |V|M[c] if |V|[c] <> 0 where V in CH G X

### Price of intermediary raw material consumption c of sector s
PCI[c, s] * CI[c, s] = PCID[c, s] * CID[c, s] + PCIM[c, s] * CIM[c, s] if CI[c, s] <> 0

### Material consumption price for sector s
PMAT[s]*F[MAT, s] = sum(PCI[cm, s]*CI[cm, s] if CI[cm, s] <> 0 on cm) if F[MAT, s] <> 0

### Energy price for sector s
PE[s]*F[E, s] = sum(PCI[ce, s]*CI[ce, s] if CI[ce, s] <> 0 on ce) if F[E, s] <> 0

### Average selling price for domestic commodity c (Value & volume)

## YQS[c] is the volume of the production expressed at market price. It should not be seen as a composite of several "goods": production at base price, margins and taxes. Its does not increase when the volume of the margins and taxes increase. The price does instead. This is equivalent to assuming that YQS[c] is always proportionnal to and YQ[c] since the volume of margins and taxes depends on the latter. Writing it following the specification composite of several goods, YQS[c] = YQ[c] + MGPD[c] + NTAXPD[c], would lead to inacurate results since a decrease in the quantity of margins used per unit of production would not lead to a decrease of the selling price. 

PYQS[c]*YQS[c] = PYQ[c]*YQ[c] + PMGPD[c]*MGPD[c] + NTAXPD_VAL[c]  if YQS[c] <> 0 

  d(log(YQS[c])) = d(log(YQ[c]))	 if YQS[c] <> 0 

### Average selling price for imported commodity c
PMS[c]*MS[c] = PM[c]*M[c] + NTAXPM_VAL[c] + PMGPM[c]*MGPM[c]  if MS[c] <> 0 

		d(log(MS[c])) = d(log(M[c]))  if MS[c] <> 0 



### Price of the margins paid to commodity cc on the domestic commodity c 
### We assume that the margins paid on domestic and imported commodities can be produced by domestic and foreign (using the import share of the margin recieved). The price of the margins paid to commodity cc is assumed commun to all commodity c.     
PMGPD[cc, c] * MGR[cc] = PMGRD[cc] * MGRD[cc] + PMGRM[cc] * MGRM[cc]   if MGPD[cc, c] <> 0

### Price of the margins paid to commodity cc on the imported commodity c
### This price is the same as the one paid on domestic commodity because of the assumption given in the previous equation.  
PMGPM[cc, c] = PMGPD[cc, c] if MGPM[cc, c] <> 0 

### Price of domesticly produced margins recieved by commodity c 
PMGRD[c] = PYQS[c] if MGRD[c] <> 0 

### Price of imported margins recieved by commodity c
PMGRM[c] = PMS[c]  if MGRM[c] <> 0 

### Price of intermediary raw material consumption domestically produced c of sector s
PCID[c, s] = PYQS[c] if CID[c, s] <> 0 

### Price of the imported intermediary raw material consumption c of sector s
PCIM[c, s] = PMS[c]  if CIM[c, s] <> 0 

PCHD[c] = PYQS[c] if CHD[c] <> 0 
PCHM[c] = PMS[c]  if CHM[c] <> 0 


PGD[c] = PYQS[c] if GD[c] <> 0 
PGM[c] = PMS[c]  if GM[c] <> 0 


PID[c, s] = PYQS[c] if ID[c, s] <> 0 
PIM[c, s] = PMS[c]  if IM[c, s] <> 0 

PXD[c] = PYQS[c] if XD[c] <> 0 
PXM[c] = PMS[c]  if XM[c] <> 0 


PDSD[c] = PYQS[c] if DSD[c] <> 0 
PDSM[c] = PMS[c]  if DSM[c] <> 0 


## Price of imported commodity c
PM[c] = TC*PWD[c] if M[c] <> 0

## Notional wage in sector s
d(log(W_n[s])) = RHO_W_Cons[s] + RHO_W_P[s]*d(log(P_e)) + RHO_W_PROG[s]*d(log(PROG_L[s])) - RHO_W_U[s] * (UnR - DNAIRU) - RHO_W_DU[s]*d(UnR) + RHO_W_L[s]*d(log(F[L, s]) - log(F[L])) if W_n[s] <> 0

# Average wage
W * F[L] = (sum(W[s] * F[L, s] on s))

# Price index (consumer price)
P = PCH

## Notional interest rate of the Central Bank (Taylor rule)
# "Original" rule
d(R_n) = RHO_Rdir_Cons + RHO_Rdir_P * d(d(P)/P{-1}) - RHO_Rdir_UnR * d(UnR) 

# Interest rate paid on capital by sector s
# We assume a constant premiun on the interest rate of the Central Bank
d(R[s]) = d(R)

# Interest rate paid by the Governement on its debt
# We assume a constant premiun on the interest rate of the Central Bank
d(R_G) = d(R)
